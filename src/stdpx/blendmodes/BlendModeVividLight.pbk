///////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2008-2009 Vincent Petithory - http://blog.lunar-dev.net/
// 
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
// 
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
// 
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE. 
///////////////////////////////////////////////////////////////////////////////

<languageVersion : 1.0;>

kernel BlendModeVividLight
<   
    namespace : "http://blog.lunar-dev.net::BlendModes";
    vendor : "Vincent Petithory";
    version : 1;
    description : "Merges two images using the Vivid Light blend mode.";
>
{
    input image4 background;
    input image4 blender;
    output pixel4 pxout;

    void evaluatePixel()
    {
        float2 uc = outCoord();
        pixel4 pxbg = sampleNearest(background,uc);
        pixel4 pxbl = sampleNearest(blender,uc);
        
        if (pxbg.a>0.0)
        {
            pxbg.rgb /= pxbg.a;
        }
        if (pxbl.a>0.0)
        {
            pxbl.rgb /= pxbl.a;
        }
        
        pixel3 result;
        
        if (pxbl.r < 0.5)
        {
            result.r = pixel1(0.0);
            if (pxbl.r > 0.0)  result.r = 1.0 - min(1.0,(1.0-pxbg.r)/(2.0*pxbl.r));
        }
        else
        {
            result.r = pixel1(1.0);
            if (2.0*(pxbl.r-0.5) < 1.0)  result.r = min(1.0,pxbg.r/(1.0-2.0*(pxbl.r-0.5)));
        }
        
        if (pxbl.g < 0.5)
        {
            result.g = pixel1(0.0);
            if (pxbl.g > 0.0)  result.g = 1.0 - min(1.0,(1.0-pxbg.g)/(2.0*pxbl.g));
        }
        else
        {
            result.g = pixel1(1.0);
            if (2.0*(pxbl.g-0.5) < 1.0)  result.g = min(1.0,pxbg.g/(1.0-2.0*(pxbl.g-0.5)));
        }
        
        if (pxbl.b < 0.5)
        {
            result.b = pixel1(0.0);
            if (pxbl.b > 0.0)  result.b = 1.0 - min(1.0,(1.0-pxbg.b)/(2.0*pxbl.b));
        }
        else
        {
            result.b = pixel1(1.0);
            if (2.0*(pxbl.b-0.5) < 1.0)  result.b = min(1.0,pxbg.b/(1.0-2.0*(pxbl.b-0.5)));
        }
        
        
        pxout.rgb = pxbg.rgb*(1.0-pxbl.a)+result*pxbl.a;
        pxout.a = 1.0;
        
    }
}
